apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

//环境配置插件。
apply plugin: "com.kelin.environment"

environment {

    online false

    initEnvironment test

    releaseConfig {
        appIcon "@mipmap/ic_launcher"
        appName "@string/app_name"
        versionName "1.0.0"
        variables APK_BUILD_TYPE: "release"
    }

    devConfig {
        appIcon "@mipmap/ic_launcher"
        appName "@string/app_name"
        versionName "1.0.0.0"
        variables APK_BUILD_TYPE: "test"
    }

    releaseEnv {
        alias "生产"

        variable "API_BASE_URL", "https://iyx-smart-api.intelliyx.com"
    }

    testEnv {
        alias "测试"

        variable "API_BASE_URL", "http://sit.tms.api.yuchuanglian.com"
    }

    demoEnv {
        alias "预发"

        variable "API_BASE_URL", "http://uat.tms.api.yuchuanglian.com"
    }
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId packageName
        minSdkVersion 17
        targetSdkVersion 29
        versionCode environment.versionCode
        versionName environment.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true
    }

    //打包自定义路径配置。
    android.applicationVariants.all {
        def channel = "yx-" + it.productFlavors[0].name.replace("yx-", "").split("-")[0]
        println("=========channel:${channel}")
        //获取当前时间的"YYYY-MM-dd"格式。assembleRelease
        def createTime = new Date().format("YYYY-MM-dd_HHmm", TimeZone.getTimeZone("GMT+08:00"))
        def buildType = it.buildType.name
        def realBuildType = environment.getVariable('APK_BUILD_TYPE')
        if (buildType == "release") {
            it.getPackageApplication().outputDirectory = new File(project.rootDir.absolutePath + "/apks/${realBuildType}")
        }
        it.outputs.all {
            if (it.outputFile != null && 'release' == buildType) {
                if (it.outputFile.name.endsWith(".apk")) {
                    //重新对apk命名。
                    it.outputFileName = "Office_${channel}_v${versionName}_${createTime}-${realBuildType}.apk"
                }
            }
        }
    }

    //支持多渠道配置。
    flavorDimensions "default"
    productFlavors {
        for (channel in file(project.CHANNEL_PROPERTIES)) {
            if (channel != null && channel.length() > 0 && !channel.startsWith("//")) {
                "$channel-$packageName" {
                    dimension "default"
                }
                if (!environment.online) {
                    break
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    android {
        lintOptions {
            abortOnError false
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation project(path: ':domain')
    implementation project(path: ':domainimpl')
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'

    //自定义相册、图片视频预览库。
    implementation 'com.github.kelinZhou:PhotoSelector:0.9.2'
    //动态权限
    implementation 'com.github.kelinZhou:OkPermission:3.1.3'
    //安装包升级库
    implementation 'com.github.kelinZhou:ApkUpdater:3.0.3'
    //轮播图
    implementation 'com.github.kelinZhou:Banner:2.7.0'
    //事件总线
    implementation 'org.greenrobot:eventbus:3.1.1'

}